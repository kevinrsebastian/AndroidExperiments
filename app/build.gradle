plugins {
    id "com.android.application"
    id "com.google.dagger.hilt.android"
    id "kotlin-kapt"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.kevinrsebastian.androidex"
    compileSdk 33

    defaultConfig {
        applicationId "com.kevinrsebastian.androidex"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.kevinrsebastian.androidex.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    sourceSets {
        androidTest.java.srcDir "src/test/common/java"
        androidTest.java.srcDir "src/test/instrumentation/java"
        test.java.srcDir "src/test/common/java"
        test.java.srcDir "src/test/unit/java"
    }
}

dependencies {
    /* AndroidX Support Commons */
    // https://developer.android.com/jetpack/androidx/versions
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.core:core-ktx:1.10.0"

    /* AndroidX Splash Screen */
    implementation "androidx.core:core-splashscreen:1.0.0"

    /* Hilt DI */
    // https://developer.android.com/training/dependency-injection/hilt-android
    def hilt_version = "2.45"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"

    /* Material Design */
    // https://material.io/develop/android/docs/getting-started
    implementation "com.google.android.material:material:1.8.0"

    /* ReactiveX */
    // https://reactivex.io
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"

    /***** Test Dependencies *****/

    /* AndroidX Core and Miscellaneous */
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"

    /* AndroidX Assertions */
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:truth:1.5.0"
    testImplementation "androidx.test.ext:truth:1.5.0"

    /* AndroidX Espresso */
    // https://developer.android.com/training/testing/espresso
    def espresso_version = "3.5.1"
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version")
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version")
    androidTestImplementation("androidx.test.espresso:espresso-intents:$espresso_version")

    /* DataFactory */
    def datafactory_version = "0.8"
    androidTestImplementation "org.fluttercode.datafactory:datafactory:$datafactory_version"
    testImplementation "org.fluttercode.datafactory:datafactory:$datafactory_version"

    /* Hamcrest Matchers */
    // http://hamcrest.org/JavaHamcrest/distributables
    androidTestImplementation "org.hamcrest:hamcrest:2.2"
    androidTestImplementation "org.hamcrest:hamcrest-library:2.2"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"

    /* JUnit */
    testImplementation "junit:junit:4.13.2"

    /* Mockito */
    def mockito_version = "5.3.0"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:5.2.0"
}
